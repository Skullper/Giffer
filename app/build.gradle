apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.a2lab.project.giftest"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    output.outputFile.name.replace("app-release", "${rootProject.name}")
            )
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

//copy release apk to custom folder
task copyApks(type: Copy) {
    from("$buildDir/outputs/apk") {
        exclude '**/*debug*'
    }
    into '/home/pugman/APKs'
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.finalizedBy 'copyApks'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    testCompile 'junit:junit:4.12'
    //kotlin
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.12'
    // sdk19, sdk21, sdk23 are also available
    compile 'org.jetbrains.anko:anko-sdk23:0.9'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //rx
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'
    compile 'io.reactivex:rxjava:1.2.2'
    //camera
    compile 'com.afollestad:material-camera:0.4.4'
    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    //design
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
}
repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
